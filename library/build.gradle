plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                // Replace with your own group ID.
                groupId 'com.mpierucci.redux-android'

                // Replace with the name of your library
                artifactId 'core'
                version '0.0.1'
            }

            debug(MavenPublication) {
                from components.debug

                // Replace with your own group ID.
                groupId 'com.mpierucci.reudx-android'

                // Replace with the name of your library
                artifactId 'core'
                version '0.0.1'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"

                url = uri("https://maven.pkg.github.com/mpierucci/Redux-Android")

                credentials {
                    username = githubProperties['github.user']
                    password = githubProperties['github.key']
                }
            }
        }
    }
}


dependencies {

    def lifeCycle = "2.4.0"
    implementation "io.arrow-kt:arrow-core:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifeCycle"

}